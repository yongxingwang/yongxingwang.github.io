// FSI with same variable for fluid and structure
// hyperelastic solid in a rotating fluid due to outer boundary rotation
load "Curvature"
load "isoline"
load "iovtk"
verbosity=0;
string outputFileName = "output/"; 
int[int] orderOut = [1,1,1,1,1,1];

real R=0.2, Area=pi*0.04;
int m=100, n,  NN=500, remesh=1, saveEach = 5;
real muf=0.01, c1=1, T=1, dt=T/NN;
real rhof=1, rhos=1;
real xc=0.5, yc=0.5;
real beta =0.005, gamma=300, eta=0.0, pen=1.e-8;

// Fluid region
border b1(t=0,1) {x=t;  y=0 ;label=1;};
border b2(t=0,1) {x=1; y=t ;label=2;};
border b3(t=1,0) {x=t;  y=1 ;label=3;};
border b4(t=1,0) {x=0; y=t ;label=4;};
//mesh Th = square(30,30);
mesh Th = buildmesh(b1(m)+b2(m)+b3(m)+b4(m));
plot(Th, wait=1);


func perio=[[4,y],[2,y],[1,x],[3,x]];

fespace Vh(Th,P2);
fespace Ph(Th, P1);
fespace Rh(Th,[P2,P2,P1],periodic=perio);

Ph p,ph;
Vh uu,u,v,uh,vh,uold=0,vold=0,d1=0,d2=0;
Vh phi, phih, phiold;
func dd=sqrt((x-xc)*(x-xc)+(y-yc)*(y-yc)) - R;
//phiold=(dd<0) ? 1 : 0 ;
phiold = 1/(1 + exp(200*dd));

macro div(u,v) ( dx(u)+dy(v) ) // EOM
macro grad(u)[dx(u),dy(u)] // EOM
macro DD(u,v)  [[2*dx(u),div(v,u)],[div(v,u),2*dy(v)]] // EOM
macro Grad(u,v)[[dx(u),dy(u)],[dx(v),dy(v)]] // EOM
macro f(u) u*(u-1)*(2*u-1) // EOM
macro df(u) ( (u-1)*(2*u-1)+u*(2*u-1)+2*u*(u-1) )//
macro CC(u) [[dx(u)*dx(u),dx(u)*dy(u)],[dy(u)*dx(u),dy(u)*dy(u)]] // EOM


/////////////////////////////////////////////////////////////////
real  mingridsize=0.01, maxgridsize=0.2;
Th = adaptmesh(Th,grad(phiold),nbvx=100000,hmin=mingridsize, ratio=1.1,periodic=perio) ;
plot(Th, wait=1);
phiold = phiold; uold=0;vold=0;d1=0;d2=0;
uold = 0.1*pi*sin(2*pi*x)*cos(2*pi*y);
vold = -0.1*pi*cos(2*pi*x)*sin(2*pi*y);
plot(Th,phiold,coef=0.1,fill=1,value=1,wait=1);
uu=sqrt(uold^2+vold^2);
plot(Th,uu,coef=0.1,fill=1,value=1,wait=1);
//////////////////////////////////////////////////////////////

varf phase(phi,phih) =
	int2d(Th)(phi*phih/dt 
	+ beta*grad(phi)'*grad(phih) 
	+ gamma* df(phiold)*phi*phih
	)
	+int2d(Th)( convect([uold,vold],-dt,phiold)*phih/dt
		    - gamma*f(phiold)*phih + gamma*df(phiold)*phiold*phih )
+ on(1,2,3,4,phi=0);

varf xi(phi,phih) = int2d(Th)(phih);


varf fsi([u,v,p],[uh,vh,ph]) =
  int2d(Th)(rhof*[u,v]'*[uh,vh]/dt- div(uh,vh)*p -div(u,v)*ph  + pen*p*ph 
	    + muf/2*trace(DD(u,v)'*DD(uh,vh))
	     + phi*dt*c1*trace((DD(u,v) - Grad(u,v)'*Grad(d1,d2) - Grad(d1,d2)'*Grad(u,v))*Grad(uh,vh)')
  )
  + on(1,3,v=0)+ on(2,4,u=0) ;



varf res([u,v,p],[uh,vh,ph]) =
	int2d(Th)(rhof*[convect([uold,vold],-dt,uold),convect([uold,vold],-dt,vold)]'*[uh,vh]/dt
		  - phi*c1*trace((DD(d1,d2) - Grad(d1,d2)'*Grad(d1,d2))*Grad(uh,vh))
		  - eta*trace(CC(phi)'*Grad(uh,vh))
  )
  + on(1,3,v=0)+ on(2,4,u=0) ;


ofstream file("area_phase.txt");
file.precision(16);

for(n=0;n<=NN;n++){
  
       int undof = Vh.ndof;
       int tndof = undof + 1;
       real[int] lm(undof);
       lm = xi(0, Vh);

       real[int] sol(tndof);
       real[int] fb(tndof);
       fb(0:undof)=0;

	matrix B = phase(Vh, Vh);
	real[int] b = phase(0, Vh);
	matrix M = [[B,lm],
		   [lm',0]];
	set(M, solver=UMFPACK);
	fb(0:undof-1)=b;
	fb(undof-1:undof)=Area;
	sol = M^-1 * fb;
	phi[] = sol(0:undof-1);
	
	phi = (phi> 1) ? 1 : phi;
	phi = (phi<0) ? 0 : phi;
	
	cout<<"Time step: "<<n<<", "<<"Area: "<<int2d(Th)(phi)/Area<<",  "<<"Multiplier: "<<sol(undof)<<endl;
        file <<int2d(Th)(phi)/Area<< endl;
	
	
	plot(Th,phi,coef=0.1,fill=1,value=1,wait=0);
	
	matrix A = fsi(Rh,Rh);
	real[int] rh = res(0,Rh);
	
	set(A,solver=UMFPACK);
	Rh [w1, w2, wp];
	w1[] = A^-1 * rh;
	u=w1; v= w2; p=wp;

	if (n % remesh == 0){
	  Th = adaptmesh(Th,grad(phi),nbvx=100000,hmin=mingridsize, ratio=1.1,periodic=perio) ;
	  u=u; v=v; p=p; phi=phi; d1=d1; d2=d2;
	}
	d1 = d1 + u*dt;
	d2 = d2 + v*dt;
	
	uold=u; vold=v; phiold=phi;
	
	 uu=sqrt(u^2+v^2);
	 plot(Th,uu,coef=0.1,fill=1,value=1,wait=0);

	if (n % saveEach == 0){
		int k=n/saveEach;
		savevtk(outputFileName+"fsi"+k+".vtk", Th, u,v,p,phi,d1,d2, dataname="u v p phi d1 d2", order=orderOut);		
	}

	
		 
}
file.flush;